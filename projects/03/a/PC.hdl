// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Register(in=next,load=changeRegister,out=current,out=out);
    Or8Way(in[0]=load,in[1]=inc,in[2]=reset,in[3..7]=false,out=changeRegister);
    Inc16(in=current,out=inced);
    Mux16(a=current,b=inced,sel=inc,out=currentOrInced);
    Mux16(a=currentOrInced,b=in,sel=load,out=currentOrIncedOrLoad);
    Mux16(a=currentOrIncedOrLoad,b=false,sel=reset,out=next);
}
